plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.1'
    id "io.github.gradle-nexus.publish-plugin" version "1.3.0"
}

apply from: "$rootDir/gradle/additional-artifacts.gradle"
apply from: "$rootDir/gradle/dependencies.gradle"
apply from: "$rootDir/gradle/publishing.gradle"
apply from: "$rootDir/gradle/release.gradle"

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains:annotations:24.0.0'
    ext.autoValueVersion = '2.0.0'
    ext.autoServiceVersion = '2.0.0'

    implementation group: 'org.springframework', name: 'spring-context', version: '6.1.5'
    implementation group: 'org.springframework', name: 'spring-webflux', version: '6.1.5'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.17.0'

    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'

    testCompileOnly 'org.projectlombok:lombok:1.18.32'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.32'

    testImplementation ('org.assertj:assertj-core:3.25.1')
    testImplementation ('ch.qos.logback:logback-core:1.4.14')
    testImplementation ('com.squareup.okhttp3:mockwebserver:4.12.0')
    testImplementation ('org.testng:testng:7.9.0')
    testImplementation ('ch.qos.logback:logback-classic:1.4.14')
}

ext.compatibilityVersion = JavaVersion.VERSION_17
ext.javadocPath = compatibilityVersion.isJava8() ? '' : 'en/java/'
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion

test {
    useTestNG()
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Jenkins REST client',
                   'Implementation-Version': archiveVersion,
                   'Built-By': System.getProperty('user.name'),
                   'Built-Date': new Date(),
                   'Built-JDK': System.getProperty('java.version'),
                   'Built-Gradle': gradle.gradleVersion
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ["-Xlint:-options"]
}

task mockTest(type: Test) {
    group = "Verification"
    description = "Mock tests"
    useTestNG()
    include '**/**MockTest*'
    maxParallelForks = 2
    testLogging {
        showStandardStreams = true
        events 'started', 'passed', 'failed'
    }
}

task integTest(type: Test, dependsOn: mockTest) {
    group = "Verification"
    description = "Integration tests - Jenkins must be running. See the README."
    doFirst {
        def integProjectDir = project.file("${buildDir}/integ-projects")
        if (!integProjectDir.exists()) {
            if (!integProjectDir.mkdirs()) {
                throw new RuntimeException("Failed to create integ-project directory @ ${integProjectDir.path}")
            }
        }
    }
    useTestNG()
    include "**/**LiveTest*"
    maxParallelForks = 1
    testLogging {
        showStandardStreams = true
        events 'started', 'passed', 'failed'
    }

    def possibleUsernameApiToken = project.findProperty("testJenkinsUsernameApiToken")
    def usernameApiToken = [:]
    if (possibleUsernameApiToken) {
        usernameApiToken["test.jenkins.usernameApiToken"] = possibleUsernameApiToken
    }

    systemProperties = [
        "test.jenkins.endpoint" : testJenkinsRestEndpoint,
        "test.jenkins.usernamePassword" : testJenkinsUsernamePassword
    ] << usernameApiToken
}

javadoc {
    source = sourceSets.main.allJava
    options.with {
        links "https://docs.oracle.com/${javadocPath}javase/${compatibilityVersion.toString().replaceAll(/.*\./,"")}/docs/api"
        addStringOption('Xdoclint:none', '-quiet')
        addStringOption('source', compatibilityVersion.toString().replaceAll(/.*\./,""))
    }
}
